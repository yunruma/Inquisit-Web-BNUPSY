// 1-首先定义用到的标签
**************************************************************************************************************
**************************************************************************************************************
<item attributeAlabel>
	/1 = "好"
</item>

<item attributeBlabel>
	/1 = "坏"
</item>

<item targetALabel>
	/1 = "白人"
</item>

<item targetBLabel>
	/1 = "黑人"
</item>

// 2-其次定义用到的刺激
**************************************************************************************************************
**************************************************************************************************************
<item attributeA>
	/1 = "善良"
	/2 = "可爱"
	/3 = "漂亮"
	/4 = "美好"
</item>

<item attributeB>
	/1 = "恐惧"
	/2 = "厌恶"
	/3 = "伤心"
	/4 = "可怕"
</item>

<item targetA>
	/1 = "白人1.jpg"
	/2 = "白人2.jpg"
	/3 = "白人3.jpg"
	/4 = "白人4.jpg"
</item>

<item targetB>
	/1 = "黑人1.jpg"
	/2 = "黑人2.jpg"
	/3 = "黑人3.jpg"
	/4 = "黑人4.jpg"
</item>

// 3-再次，定义用到的指导语
***************************************************************************************************
**************************************************************************************************************
<item begininstruction>
            /1 ="欢迎参加本次实验"
</item>

<item introinstructions>
	/ 1 = "在本任务中，请您根据呈现的图片或词语做出按键反应，如果呈现的是<%item.attributeALabel.item.1%>的词语，或是<%item.targetALabel.1%> ，<%item.targetBLabel.1%>之中某个特定类别的图片，请您按F键，如果呈现的是<%item.attributeBLabel.item.1%>中的词语或是<%item.targetALabel.1%> ， <%item.targetBLabel.1%>中不是之前指定的类别的图片，请您按J键。请您在最短的时间内尽量正确地进行反应。在接下来的练习阶段中，请您熟悉按键的设定。当您出现错误时，屏幕上会显示“×”。"
	/ 2 = "对于 <%item.attributeALabel.item.1%>类别的词汇请按F键，其他词汇请按J键。请按空格键继续。"
</item>

<item endinstruction>
           / 1="实验结束，谢谢！"
</item>

// 具体到每个block里，要求按键反应都是不一样的，可以通过一个指导语定义，根据具体的来定义具体的类型，例如下面的这个定义，用currentTarget变量来定义要求反应的类别，这个变量在block开始的时候进行赋值，那么在指导语里就能对应显示了。

<item testinstructions>
	/ 1 = "当屏幕上呈现<%item.attributeALabel.item.1%>类的词汇或<%values.currentTarget%>类的图片时，请按F键，当屏幕上呈现其他类别的词汇或图片时，请按J键。请按空格键开始。"
</item>


// 指导语界面上还需要呈现给被试各个类别都有哪些词汇，呈现方法如下，也是采用item引用的方式。因为这个部分后面也需要根据需要呈现
<item attributeAFocusInstruct>
	/ 1 = "<%item.attributeALabel.1%>~n<%item.attributeA.1%> <%item.attributeA.2%> <%item.attributeA.3%> <%item.attributeA.4%>"
</item>

<item targetAFocusInstruct>
	/ 1 = "<%item.targetALabel.1%>~n<%item.targetA.1%> <%item.targetA.2%> <%item.targetA.3%> <%item.targetA.4%>"
</item>

<item targetBFocusInstruct>
	/ 1 = "<%item.targetBLabel.1%>~n<%item.targetB.1%> <%item.targetB.2%> <%item.targetB.3%> <%item.targetB.4%>"
</item>


// 4-到这里，我们已经把需要呈现的刺激内容都填写完整了，现在需要定义刺激呈现的位置，刺激呈现的形式，我们要根据之前画的图纸
//   仔细区分类别，分别进行定义。

**************************************************************************************************************
**************************************************************************************************************
// 首先我们来定义指导语的呈现方式
<text begininstruction>
/ items = begininstruction 
/ size = (90%, 50%)
/ position = (50%, 40%)
/ select = sequence
/ resetinterval = 20
/ valign = top
/ hjustify = center
/ vjustify = top
</text>

<text introinstructions>
/ items = introinstructions
/ size = (90%, 50%)
/ position = (50%, 40%)
/ select = sequence
/ resetinterval = 20
/ valign = top
/ hjustify = center
/ vjustify = top
</text>

<text testInstructions>
/ items = testInstructions
/ size = (90%, 50%)
/ position = (50%, 50%)
/ valign = top
/ hjustify = center
/ vjustify = top
</text>

<text endInstruction>
/ items = endInstruction
/ size = (90%, 50%)
/ position = (50%, 50%)
/ valign = top
/ hjustify = center
/ vjustify = top
</text>

// 指导语中需要显示 I 按键针对的刺激类别，因此这个地方是因block而定的，所以可以先把格式设置好，具体item内容可以通过values.currentTarget进行定义调用。

// items先设为空，在block中进行定义。

<text topFocusInstruct>
/ items = ("")
/ valign = bottom
/ halign = center
/ position = (50%, 15%)
/ size = (100%, 12%)
/ txcolor = aqua
/ fontstyle = ("Arial", 5%)
</text>

<text orInstruct>
/ items = ("or")
/ valign = center
/ halign = center
/ position = (50%, 17.5%)
/ txcolor = white
/ fontstyle = ("Arial", 3%)
</text>

<text bottomFocusInstruct>
/ items = attributeAFocusInstruct
/ valign = top
/ halign = center
/ position = (50%, 20%)
/ size = (100%, 12%)
/ txcolor = yellow
/ fontstyle = ("Arial", 5%)
</text>


// 之后我们来定义刺激词汇的呈现方式
<text attributeA>
/ items = attributeA
/ fontstyle = ("宋体", 10%)
/ txcolor = yellow
</text>

<text attributeB>
/ items = attributeB
/ fontstyle = ("宋体", 10%)
/ txcolor = yellow
</text>

<picture targetA>
    /items = targetA
	/erase = True(0,0,0) 
	/position = (50,50)
	/size = (600,450)
</picture>

<picture targetB>
    /items = targetB
    /erase = True(0,0,0)
    /position = (50,50)
    /size = (600,450)
</picture>

<text error>
/ position = (50%, 75%)
/ items = ("Ã")
/ color = red
/ fontstyle = ("Arial", 10%)
</text>

// 在每个block中，都需要把正确按键反应对应的属性类别放在最上方，这个地方刺激呈现的方式也是需要定义的。

// 这个放在第一页指导语上

<text attributeAFocusLabel>
/ items = attributeALabel
/ valign = top
/ halign = center
/ position = (50%, 5%)
/ txcolor = yellow
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

// 这三个放在每个block开头的指导语上

<text targetFocusLabel>
/ items = ("<%values.currentTarget%>")
/ valign = top
/ halign = center
/ position = (50%, 5%)
/ txcolor = aqua
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

<text orLabel>
/ items = ("or")
/ valign = top
/ halign = center
/ position = (50%, 10%)
/ txcolor = white
/ fontstyle = ("Arial", 3%)
/ erase = false
</text>


<text orAttributeAFocusLabel>
/ items = attributeALabel
/ valign = top
/ halign = center
/ position = (50%, 15%)
/ txcolor = yellow
/ fontstyle = ("Arial", 4%)
/ erase = false
</text>

<text progress>
/ position = (50%, 95%)
/ items = ("(Block <%expressions.currentblocknumber%> of <%expressions.totalblockcount %>)")
/ color = white
/ fontstyle = ("Arial", 2%)
</text>

<shape eraseLabels>
/ color = black
/ shape = rectangle
/ size = (50%, 20%)
/ position = (50%,  10%)
</shape>


// 5- 前面已经定义所需要的刺激呈现方式，现在就可以放trial啦
**************************************************************************************************************
**************************************************************************************************************
<trial attributeA>
/ validresponse = ("J", "F")
/ correctresponse = ("F")
/ stimulusframes = [1 = attributeA]
/ posttrialpause = 250
</trial>

<trial attributeB>
/ validresponse = ("J", "F")
/ correctresponse = ("J")
/ stimulusframes = [1 = attributeB]
/ posttrialpause = 250
</trial>

<trial targetALeft>
/ validresponse = ("J", "F")
/ correctresponse = ("J")
/ stimulusframes = [1 = targetA]
/ posttrialpause = 250
</trial>

<trial targetARight>
/ validresponse = ("J", "F")
/ correctresponse = ("F")
/ stimulusframes = [1 = targetA]
/ posttrialpause = 250
</trial>

<trial targetBLeft>
/ validresponse = ("J", "F")
/ correctresponse = ("J")
/ stimulusframes = [1 = targetB]
/ posttrialpause = 250
</trial>

<trial targetBRight>
/ validresponse = ("J", "F")
/ correctresponse = ("F")
/ stimulusframes = [1 = targetB]
/ posttrialpause = 250
</trial>

<trial introinstructions>
/ ontrialbegin = [if ( script.currentblock == "attributes" ) {text.bottomFocusInstruct.1 = item.attributeAFocusInstruct.1; values.currentTarget = item.attributeAlabel.1;} ]
/ stimulustimes = [1=introinstructions, bottomFocusInstruct, progress]
/ correctresponse = (" ")
/ errormessage = false
/ recorddata = false
</trial>

<trial testinstructions>
/ stimulustimes = [1=testinstructions, topFocusInstruct, orInstruct, bottomFocusInstruct, progress]
/ correctresponse = (" ")
/ errormessage = false
/ recorddata = false
</trial>

<trial showAttributeLabel>
/ stimulusframes = [1=attributeAFocusLabel]
/ response = noresponse
/ recorddata = false
</trial>

<trial showLabels>
/ stimulusframes = [1=targetFocusLabel, orLabel, orAttributeAFocusLabel]
/ response = noresponse
/ recorddata = false
</trial>

<trial eraseLabels>
/ stimulusframes = [1=eraseLabels]
/ response = noresponse
/ recorddata = false
</trial>

<trial summary>
/ ontrialbegin = [values.magnitude = "没有或几乎没有"]
/ ontrialbegin = [if( abs(expressions.d) > 0.15 ) values.magnitude = "轻微"]
/ ontrialbegin = [if( abs(expressions.d) > 0.35 ) values.magnitude = "中等"]
/ ontrialbegin = [if( abs(expressions.d) >= 0.65 ) values.magnitude = "强烈"]
/ ontrialbegin = [if (expressions.d >= 0.0) expressions.preferred = item.targetALabel.1]
/ ontrialbegin = [if (expressions.d < 0.0) expressions.preferred = item.targetBLabel.1]
/ ontrialbegin = [if (expressions.d < 0.0) expressions.notpreferred= item.targetALabel.1]
/ ontrialbegin = [if (expressions.d >= 0.0) expressions.notpreferred= item.targetBLabel.1]
/ stimulustimes = [0=summary]
/ validresponse = (" ")
/ recorddata = false
/ skip = [values.skipsummary]
</trial>

<text summary>
/ items = ("Your IAT score (D) was <% expressions.d %>, which suggests <% values.magnitude %> automatic preference for <% expressions.preferred %> compared to <% expressions.notpreferred %>.~n~n~nPress the spacebar to complete this session.")  
/ size = (60%, 80%)
/ hjustify = left
 </text>

<trial begininstruction>
/validresponse=(anyresponse)
/stimulusframes = [1=begininstruction]
/ posttrialpause = 250
</trial>

<trial endinstruction>
/validresponse=(anyresponse)
/stimulusframes = [1=endinstruction]
/ posttrialpause = 250
</trial>

<block begininstruction>
   /screencolor = (0,0,0)
   /trials=[1=begininstruction]
</block>

<block intro>
/ trials = [1 = introinstructions]
</block>

<block exe_a>
/ onblockbegin = [values.currentTarget = item.targetALabel.1; text.topFocusInstruct.1 = item.targetAFocusInstruct.1]
/ trials = [1=testInstructions; 2=showLabels;
  3-6 = noreplace(targetARight, targetBLeft);
  8,10,12,14 = noreplace(targetARight, targetBLeft);
  7,9,11,13 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
</block>

<block exe_b>
/ onblockbegin = [values.currentTarget = item.targetBLabel.1; text.topFocusInstruct.1 = item.targetBFocusInstruct.1]
/ trials = [1=testInstructions; 2=showLabels;
  3-6 = noreplace(targetBRight, targetALeft);
  8,10,12,14 = noreplace(targetBRight, targetALeft);
  7,9,11,13 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
</block>


<block A>
/ skip = [values.extended == false && block.A.totalcount >=1]
/ onblockbegin = [values.currentTarget = item.targetALabel.1; text.topFocusInstruct.1 = item.targetAFocusInstruct.1]
/ trials = [1=testInstructions; 2=showLabels;
  3-6 = noreplace(targetARight, targetBLeft);
  8,10,12,14,16,18,20,22 = noreplace(targetARight, targetBLeft);
  7,9,11,13,15,17,19,21 = noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1sum =  values.A1sum + block.A.latency]
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1n += 1]
/ ontrialend = [if (block.A.totalcount == 0 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A1ss =  values.A1ss  + (block.A.latency * block.A.latency)]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2sum =  values.A2sum + block.A.latency]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2n += 1]
/ ontrialend = [if (block.A.totalcount == 1 && block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.A2ss =  values.A2ss  + (block.A.latency * block.A.latency)]
/ ontrialend = [if (block.A.latency  <= 10000 && block.A.currenttrialnumber >= 7 ) values.n_correct += block.a.correct]
</block>

<block B>
/ skip = [values.extended == false && block.B.totalcount >=1]
/ onblockbegin = [values.currentTarget = item.targetBLabel.1; text.topFocusInstruct.1 = item.targetBFocusInstruct.1]
/ trials = [1=testInstructions;2=showLabels;
  3-6 = noreplace(targetBRight, targetALeft);
  8,10,12,14,16,18,20,22= noreplace(targetBRight, targetALeft);
  7,9,11,13,15,17,19,21= noreplace(attributeA, attributeB)]
/ errormessage = true(error,200)
/ responsemode = correct
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1sum =  values.B1sum + block.B.latency]
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1n +=  1]
/ ontrialend = [if (block.B.totalcount == 0 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B1ss =  values.B1ss  + (block.B.latency * block.B.latency)]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2sum =  values.B2sum + block.B.latency]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2n +=  1]
/ ontrialend = [if (block.B.totalcount == 1 && block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.B2ss =  values.B2ss  + (block.B.latency * block.B.latency)]
/ ontrialend = [if (block.B.latency  <= 10000 && block.B.currenttrialnumber >= 7 ) values.n_correct += block.B.correct]
</block>

<block summary>
/ trials = [1=summary]
/ recorddata = false
</block>



<expt>
/ subjects = (1 of 2)
/ groupassignment = groupnumber
/ blocks = [1 = intro; 2= exe_a; 3 = exe_b; 4=A; 5=B; 6=A; 7=B; 8=summary]
/onexptend = [values.completed = 1]
</expt>

<expt>
/ subjects = (2 of 2)
/ groupassignment = groupnumber
/ blocks = [1 = intro; 2= exe_b; 3 = exe_a; 4=B; 5=A; 6=B; 7=A; 8=summary]
/onexptend = [values.completed = 1]
</expt>


<block endinstruction>
   /screencolor = (0,0,0)
   /trials=[1=endinstruction]
</block>

*************************************************************************************************************************************************************************************************************************************************

定义data file
***********************************************************************
<data>
/file = "Briefprejudice_rawdata.iqdat"
/ columns = [date, time, group, subject, blockcode, blocknum, trialcode, trialnum, response, correct, latency, 
					stimulusnumber, stimulusitem, expressions.d1, expressions.d2, expressions.d, expressions.percentcorrect]
/separatefiles = true
</data>



<summarydata >
/file = "Briefprejudice_summary.iqdat"
/columns = [script.filename, script.startdate, script.starttime, script.subjectid, script.groupid, script.elapsedtime, values.completed,
expressions.d1, expressions.d2, expressions.d, expressions.percentcorrect]
</summarydata>


<values>
/completed = 0
/ A1sum = 0
/ A2sum = 0
/ B1sum = 0
/ B2sum = 0
/ A1n = 0
/ A2n = 0
/ B1n = 0
/ B2n = 0
/ A1ss = 0
/ A2ss = 0
/ B1ss = 0
/ B2ss = 0
/ magnitude = "unknown"
/ currentTarget = ""
/ n_correct = 0
</values>



<expressions>
/ A1m = values.A1sum / values.A1n
/ A2m = values.A2sum / values.A2n
/ B1m = values.B1sum / values.B1n
/ B2m = values.B2sum / values.B2n
/ A1sd = sqrt((values.A1ss - (values.A1n* (expressions.A1m * expressions.A1m))) / (values.A1n - 1))
/ A2sd = sqrt((values.A2ss - (values.A2n* (expressions.A2m * expressions.A2m))) / (values.A2n - 1))
/ B1sd = sqrt((values.B1ss - (values.B1n* (expressions.B1m * expressions.B1m))) / (values.B1n - 1))
/ B2sd = sqrt((values.B2ss - (values.B2n* (expressions.B2m * expressions.B2m))) / (values.B2n - 1))
/ sd1 = sqrt((((values.A1n - 1) * (expressions.A1sd * expressions.A1sd) + (values.B1n - 1) * (expressions.B1sd * expressions.B1sd)) + ((values.A1n + values.B1n) * ((expressions.A1m - expressions.B1m) * (expressions.A1m - expressions.B1m)) / 4) ) / (values.A1n + values.B1n - 1) )
/ sd2 = sqrt((((values.A2n - 1) * (expressions.A2sd * expressions.A2sd) + (values.B2n - 1) * (expressions.B2sd * expressions.B2sd)) + ((values.A2n + values.B2n) * ((expressions.A2m - expressions.B2m) * (expressions.A2m - expressions.B2m)) / 4) ) / (values.A2n + values.B2n - 1) )
/ d1 = (expressions.B1m - expressions.A1m) / expressions.sd1
/ d2 = (expressions.B2m - expressions.A2m) / expressions.sd2
/ d = if ( values.extended ) { (d1+d2) / 2 } else { d1 }
/ currentblocknumber = max(expt.1.currentblocknumber, expt.2.currentblocknumber)
/ preferred = "unknown"
/ notpreferred = "unknown"
/ totalblockcount = if (values.extended) {expt.1.blockcount} else {expt.1.blockcount-2}
/ percentcorrect = (values.n_correct/ (values.a1n + values.a2n + values.b1n + values.b2n)) * 100
</expressions>



<defaults>
/ fontstyle = ("Arial", 3.5%)
/ screencolor = black
/ txbgcolor = black
/ txcolor = white
/ minimumversion = "4.0.0.0"
</defaults>


<values>
/ skipsummary = false
/ extended = true
</values>

